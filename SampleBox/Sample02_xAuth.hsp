/*======================================================================
    xAuth認証サンプル
------------------------------------------------------------------------
    xAuth認証のサンプルです。
    xAuth認証は、ユーザ名（スクリーン名）とパスワード等を引数に指
  定してAPI"oauth/access_token"にアクセスするだけでAccessTokenと
  AccessTokenSecretを取得することが出来ます。しかし、xAuthを使うには
  Twitter側からの許可が必要です。詳しくはモジュールと同封されている
  「TsubuyakiSoup.html」を参照してください。

  ※このモジュールやサンプルを実行する場合には、Twitter側から
    ConsumerKeyとConsumerSecretを取得する必要があります。詳しくは、モ
    ジュールと同封されている「TsubuyakiSoup.html」を参照してください。
------------------------------------------------------------------------
Author     : takata
LastUpdate : 10/10/30
CreateDate : 10/09/23
======================================================================*/

// モジュールをインクルード
#include "./TsubuyakiSoup.as"

; サンプル用設定ファイル
#include "SampleSetting.hsp"


//---------------------------------
// 初期化処理

	TS_Init "TsubuyakiSoup Sample", CONSUMER_KEY, CONSUMER_SECRET, 20

//---------------------------------


	mes "下記の項目を入力してください。"
	UserName = ""
	PassWord = ""
	pos 5, 20
	mes "ユーザ名（メールアドレス）"
	pos 215, 20
	input UserName, 200, 18
	pos 5, 40
	mes "パスワード"
	pos 215, 40
	input PassWord, 200, 18
	pos 350
	button "認　証", *Auth
stop


*Auth
	if (UserName = "") or (PassWord = "") : dialog "ユーザ名かパスワードが入力されていません。" : stop

//---------------------------------
// xAuthでのAccessTokenとAccessTokenSecretの取得
//    GetxAuthToken 命令でAccessTokenとAccessTokenSecretを取得します。

	GetxAuthToken xToken, xSecret, UserInfoTmp, UserName, PassWord
	; エラー判断
	if stat != 200 {
		dialog "アクセストークンの取得に失敗しました。終了します。\nステータスコード : "+ stat, 0, "エラー"
		end
	}

//    SetAccessToken 命令でAccessTokenとAccessTokenSecretを設定します。
	SetAccessToken xToken, xSecret

//---------------------------------


	pos 0
	mes ""
	mes "アクセストークン取得"
	mes "AccessToken : "+ xToken
	mes "AccessTokenSecret : "+ xSecret
	mes "ユーザ情報 : "+ UserInfoTmp


//---------------------------------
// トークンの保存
//    アクセストークンは、セキュリティの観点から暗号化して保存しておくことを推奨します。

	notesel SettingFile
	noteadd xToken, 0, 0
	noteadd xSecret, 1, 0
	noteadd UserInfoTmp, 2, 0
	if (Encryption(SettingFile, "SampleKey", "Setting.txt") = 0) : dialog "ファイルの暗号化に失敗したため、ファイルは保存されませんでした。" : stop

//---------------------------------

stop
