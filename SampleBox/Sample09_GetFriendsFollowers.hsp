/*======================================================================
    フレンド＆フォロワー取得サンプル
------------------------------------------------------------------------
    フレンド＆フォロワーを取得するサンプルです。
    (このサンプルを実行するには、「Sample01_OAuth.hsp」か
    「Sample02_xAuth.hsp」を実行して「Setting.txt」を作成してくださ
    い。)

  ※このモジュールやサンプルを実行する場合には、Twitter側から
    ConsumerKeyとConsumerSecretを取得する必要があります。詳しくは、モ
    ジュールと同封されている「TsubuyakiSoupドキュメント」を参照して
    ください。
------------------------------------------------------------------------
Author     : takata
LastUpdate : 10/10/16
CreateDate : 10/10/16
======================================================================*/

// モジュールをインクルード
#include "../TsubuyakiSoup.as"

;サンプル用設定ファイル
#include "SampleSetting.hsp"

	// 初期化
	TS_Init "",CONSUMER_KEY, CONSUMER_SECRET,20

	// 設定ファイルの読み込み(ジャンプ先はSampleSetting.hsp内にあります。)
	gosub *FileRead

	// MSXML COMの初期化
	newcom oDom,"Microsoft.XMLDOM"
	oDom("async")="False"

	// 変数初期化
	//  対象ユーザ
	ObjectUserName = ScreenName
	ObjectUserID = UserID
	//  Friends
	lb_sel(0) = 0					//リストボックスの状態
	lb_elm(0) = ""					//リストボックスの内容
	//  Followers
	lb_sel(1) = 0					//リストボックスの状態
	lb_elm(1) = ""					//リストボックスの内容

	// GUI構成
	pos 5, 5
	mes "対象ユーザ名 : "
	pos 140, 5
	input ObjectUserName, 150, 20
	pos 300, 3
	button gosub "調べる！",*l_GetFandF
	//  Friends
	pos 5, 40
	mes ""+ ObjectUserName +"のFriends"
	objsize 200,100
	listbox lb_sel(0), 200, lb_elm(0)
	lb_id(0) = stat
	//  Followers
	pos 305, 40
	mes ""+ ObjectUserName +"のFollowers"
	objsize 200,100
	listbox lb_sel(1), 200, lb_elm(1)
	lb_id(1) = stat
stop



*l_GetFandF
	dialog "取得には時間がかかります。実行しますか？", 3, "確認"
	if stat != 6 : return
	title "取得中..."

	//  Friends
	Cursor = "-1"
	notesel lb_elm(0)
	notecnt = 0
	repeat 100
		GetFriends Cursor, ObjectUserName
	
		oDom->"loadXML" getBody()
		oRoot = oDom("documentElement")
		if varuse(oRoot)=0 : dialog "MSXMLの初期化に失敗しました。"
		comres elmtmp
		oDom->"SelectNodes" "/users_list/users/user/screen_name"
		repeat elmtmp("length")
			node = elmtmp("item",cnt)
			noteadd node("text"), notecnt
			notecnt++
		loop
		
		oDom->"SelectNodes" "/users_list/next_cursor"
		node = elmtmp("item",0)
		Cursor = node("text")
		if Cursor = "0" or Cursor = "" : break
	loop
	noteunsel


	//  Followers
	Cursor = "-1"
	notesel lb_elm(1)
	notecnt = 0
	repeat 100
		GetFollowers Cursor, ObjectUserName
	
		oDom->"loadXML" getBody()
		oRoot = oDom("documentElement")
		if varuse(oRoot)=0 : dialog "MSXMLの初期化に失敗しました。"
		comres elmtmp
		oDom->"SelectNodes" "/users_list/users/user/screen_name"
		repeat elmtmp("length")
			node = elmtmp("item",cnt)
			noteadd node("text"), notecnt
			notecnt++
		loop
		
		oDom->"SelectNodes" "/users_list/next_cursor"
		node = elmtmp("item",0)
		Cursor = node("text")
		if Cursor = "0" or Cursor = "" : break
	loop
	noteunsel

	objprm lb_id(0), lb_elm(0)
	objprm lb_id(1), lb_elm(1)


	title "取得完了"
return
