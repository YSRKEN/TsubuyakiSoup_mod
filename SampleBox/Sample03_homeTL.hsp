/*======================================================================
    ホームタイムライン取得サンプル
------------------------------------------------------------------------
    ホームタイムラインを取得するサンプルです。
    (このサンプルを実行するには、「Sample01_OAuth.hsp」か
    「Sample02_xAuth.hsp」を実行して「Setting.txt」を作成してくださ
    い。)

  ※このモジュールやサンプルを実行する場合には、Twitter側から
    ConsumerKeyとConsumerSecretを取得する必要があります。詳しくは、モ
    ジュールと同封されている「TsubuyakiSoupドキュメント」を参照して
    ください。
------------------------------------------------------------------------
Author     : takata
LastUpdate : 10/10/16
CreateDate : 10/09/23
======================================================================*/

// モジュールをインクルード
#include "./TsubuyakiSoup.as"

;サンプル用設定ファイル
#include "SampleSetting.hsp"

//---------------------------------
// 初期化処理

	TS_Init "TsubuyakiSoup Sample", CONSUMER_KEY, CONSUMER_SECRET, 20

//---------------------------------



// 設定ファイルの有無確認( *FileRead は SampleSetting.hsp に定義してあります。 )
gosub *FileRead



//---------------------------------
// ホームタイムラインの取得
//    SetFormatType 命令で取得形式にXMLを指定します。
//    ホームタイムラインは、GetHomeTL 命令で取得します。
//    取得する件数を第一引数に指定してください。
	SetFormatType FORMAT_XML
	GetHomeTL 50
	rawText = getBody()
	; エラー判断
	if stat != 200 {
		dialog "タイムラインの取得に失敗しました。終了します。\nステータスコード : "+ stat, 0, "エラー"
		end
	}
//    返ってくる文字列は、UTF-8のXML形式です。XMLパーサ等で解析してください。
//---------------------------------

	newcom oDom,"Microsoft.XMLDOM"
	oDom("async")="False"
	oDom->"loadXML" rawText
	oRoot = oDom("documentElement")
	if varuse(oRoot)=0 : dialog "MSXMLの初期化に失敗しました。" : end
	; テキスト
	comres elm_text
	oDom->"SelectNodes" "/statuses/status/text"
	; スクリーン名
	comres elm_screen_name
	oDom->"SelectNodes" "/statuses/status/user/screen_name"

	ListStr = ""

	repeat elm_text("length")
		; テキスト
		node = elm_text("item", cnt)
		text = node("text")
		; スクリーン名
		node = elm_screen_name("item", cnt)
		screen_name = node("text")
		
		ListStr += screen_name +" : "+ text +"\n"
	loop

	mesbox ListStr, ginfo_winx, ginfo_winy, 4

stop

